Process 'Starten volgende behandelstap'

The following applies:
    For the GEKOZEN_PATIENT in "patient waarvoor de volgende behandelstap moet starten" applies:
        if "de therapie van de patient is nog niet voltooid"
        then
            "de volgende behandelstap is aangemaakt"
            and
            "een notificatie voor de nieuwe behandelstap is aangemaakt voor de patient"

"de therapie van de patient is nog niet voltooid" =
    max(GEKOZEN_PATIENT.BEHANDELTRAJECT.STAPPEN.volgnummer) < max(GEKOZEN_PATIENT.BEHANDELTRAJECT.THERAPIE.STAPPEN.volgnummer)

"de volgende behandelstap is aangemaakt" =
    For the GEKOZEN_PATIENT in "patient waarvoor de volgende behandelstap moet starten" applies:
        One NIEUWE_BEHANDELSTAP exists in BEHANDELSTAPPEN with:
            volgnummer          = max(GEKOZEN_PATIENT.BEHANDELTRAJECT.STAPPEN.volgnummer) + 1
            status              = 'loopt'
            VORIGE              = one(GEKOZEN_PATIENT.BEHANDELTRAJECT.STAPPEN[volgnummer == max(GEKOZEN_PATIENT.BEHANDELTRAJECT.STAPPEN.volgnummer)])
            BEHANDELTRAJECT     = GEKOZEN_PATIENT.BEHANDELTRAJECT
            THERAPIESTAP        = one(GEKOZEN_PATIENT.BEHANDELTRAJECT.THERAPIE.STAPPEN[volgnummer == NIEUWE_BEHANDELSTAP.volgnummer])
            AFSPRAAK            = "nieuwe afspraak voor volgende behandelstap"

"nieuwe afspraak voor volgende behandelstap" =
    One NIEUWE_AFSPRAAK exists in AFSPRAKEN with:
        status      = 'In te plannen'

"een notificatie voor de nieuwe behandelstap is aangemaakt voor de patient" =
    One NOTIFICATIE_VOLG_BEHANDELSTAP exists in MESSAGES with:
        title       = 'Volgende afspraak is aangevraagd'
        body        = NIEUWE_AFSPRAAK.uitgebreide_samenvatting
        user        = GEKOZEN_PATIENT.GEBRUIKER
    and
    One SMS_VOLG_BEHANDELSTAP exists in SMS_MESSAGES with:
        from    = 'Prinses Maxima Centrum'
        to      = "SMS-nummer voor patient {GEKOZEN_PATIENT}"
        body    = NIEUWE_AFSPRAAK.uitgebreide_samenvatting

"patient waarvoor de volgende behandelstap moet starten" =
    one(PATIENTEN[patientidentificatienummer == input from ARTS based on PATIENTEN[BEHANDELTRAJECT.status == 'loopt']])

