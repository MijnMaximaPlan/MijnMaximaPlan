Process 'Reusable definitions'

description 'Domeinmodel voor Project Roadmap Prinses Maxima Centrum.'

PATIENTEN
description 'Concept Patient uit ZIBS.'
=
    patientidentificatienummer  : text //optional
    geboortedatum               : timestamp required
    geslacht                    : ['UN', 'M', 'F', 'UNK'] required
    meerling_indicator          : boolean //optional
    overlijdens_indicator       : boolean //optional
    datum_overlijden            : timestamp //optional
    NAAMGEGEVENS                : NAAMGEGEVENS(1) required
    ADRESGEGEVENS               : ADRESGEGEVENS(*) /* optional */ opposite of PATIENT
    CONTACTGEGEVENS             : CONTACTGEGEVENS(*) /* optional */ opposite of PATIENT
/* Toevoegen: ouders/verzorgers; contactvoorkeuren */
    OUDERS_VERZORGERS           : CONTACTPERSONEN(*) /* optional */ opposite of PATIENT
    GEBRUIKER                   : USERS(1)
    BEHANDELTRAJECT             : BEHANDELTRAJECTEN(1) /* optional */ opposite of PATIENT
    eerstvolgende_afspraak      : text = "volgende afspraak in {BEHANDELTRAJECT} tekstueel"
    next_appointment            : text = "next appointment {BEHANDELTRAJECT} textually"

NAAMGEGEVENS
description 'Concept Naamgegevens uit ZIBS.'
=
    voornamen                   : text //optional
    initialen                   : text //optional
    roepnaam                    : text //optional
    naamgebruik                 : ['NL1', 'NL2', 'NL3', 'NL4', 'UNK'] //optional // NL1 = Eigen geslachtsnaam, etc.
    GESLACHTSNAAM               : GESLACHTSNAMEN(1) //optional
    GESLACHTSNAAMPARTNER        : GESLACHTSNAMEN(1) //optional

GESLACHTSNAMEN
description 'Concept GeslachtsNaam, GeslachtsnaamPartner uit ZIBS.'
=
    voorvoegsels                : text //optional
    achternaam                  : text required

ADRESGEGEVENS
description 'Concept Adresgegevens uit ZIBS.'
=
    straat                      : text //optional
    huisnummer                  : text //optional
    huisnummerletter            : text //optional
    huisnummertoevoeging        : text //optional
    aanduiding_bij_nummer       : ['to', 'by'] //optional // staatvoor tegenover en bij
    postcode                    : text //optional
    woonplaats                  : text //optional
    gemeente                    : text //optional
    land                        : text //optional // Land GBA of ISO codelijst
    additionele_informatie      : text //optional
    adres_soort                 : ['PST', 'HP', 'PHYS', 'TMP', 'WP', 'HV'] //optional // bijv. PST postal address, HP primary home, etc.
    PATIENT                     : PATIENTEN opposite of ADRESGEGEVENS
    CONTACTPERSOON              : CONTACTPERSONEN(1) opposite of ADRESGEGEVENS

CONTACTGEGEVENS
description 'Concept Contactgegevens uit ZIBS.'
=
    TELEFOONNUMMERS             : TELEFOONNUMMERS(*) /* optional */ opposite of CONTACTGEGEVEN
    EMAILADRESSEN               : EMAILADRESSEN(*) /* optional */ opposite of CONTACTGEGEVEN
    PATIENT                     : PATIENTEN(1) opposite of CONTACTGEGEVENS
    CONTACTPERSOON              : CONTACTPERSONEN(1) opposite of CONTACTGEGEVENS

TELEFOONNUMMERS
description 'Concept Telefoonnummers uit ZIBS.'
=
    telefoonnummer              : text required
    nummer_soort                : ['HP', 'TMP', 'WP', 'MC', 'PG'] required // bijv. HP primary home, WP Work Place, MC mobile contact, etc.
    CONTACTGEGEVEN              : CONTACTGEGEVENS(1) opposite of TELEFOONNUMMERS

EMAILADRESSEN
description 'Concept Emailadressen uit ZIBS.'
=
    emailadres                  : text required
    emailsoort                  : ['HP', 'WP'] required
    CONTACTGEGEVEN              : CONTACTGEGEVENS(1) opposite of EMAILADRESSEN

CONTACTPERSONEN
description 'Concept Contactpersonen uit ZIBS; wordt gebruikt om m.n. ouders te modelleren.'
=
    NAAMGEGEVENS                : NAAMGEGEVENS(1) required
    ADRESGEGEVENS               : ADRESGEGEVENS(*) /* optional */ opposite of CONTACTPERSOON
    CONTACTGEGEVENS             : CONTACTGEGEVENS(*) /* optional */ opposite of CONTACTPERSOON
    PATIENT                     : PATIENTEN(1) required opposite of OUDERS_VERZORGERS
    rol                         : text(2) // 1 = 1e contactpersoon, 2 = 2e contactpersoon, 11 = voogd; etc.
    relatie                     : text(10) // FTH = father, MTH = mother
    GEBRUIKER                   : USERS(1) //optional

BEHANDELTRAJECTEN
description 'Verzamelconcept van alle behandelstappen van een patient in de context van één bepaald traject.'
=
    PATIENT                     : PATIENTEN(1) required opposite of BEHANDELTRAJECT
    STAPPEN                     : BEHANDELSTAPPEN(*) /* optional */ opposite of BEHANDELTRAJECT
    THERAPIE                    : THERAPIEEN(1) /* optional */
    startdatum                  : date required

BEHANDELSTAPPEN
description 'Komt overeen met een enkel contactmoment; vallen onder een BEHANDELING; hier worden alleen opgenomen de stappen die al zijn uitgevoerd, of zijn ingepland/voorgesorteerd.'
=
    verrichting                 : text required
    volgnummer                  : integer required
    status                      : text
    VORIGE                      : BEHANDELSTAPPEN(1) opposite of VOLGENDE
    VOLGENDE                    : BEHANDELSTAPPEN(1) opposite of VORIGE
    BEHANDELTRAJECT             : BEHANDELTRAJECTEN(1) required opposite of STAPPEN
    THERAPIESTAP                : THERAPIESTAPPEN(1)
    AFSPRAKEN                   : AFSPRAKEN(*) /* optional */ opposite of BEHANDELSTAP

THERAPIEEN
description 'Definitie van een kuur, bijv. een bepaalde soort chemotherapie. Feitelijk een tijdelijke vervanging van PRACTOCOL oid.'
=
    soort                       : text
    STAPPEN                     : THERAPIESTAPPEN(*) /* optional */ opposite of THERAPIE

THERAPIESTAPPEN
description 'Stappen die onderdeel uitmaken van een kuur.'
=
    verrichting                 : text required
    volgnummer                  : integer required
    interval                    : text required // bijv. '3 weeks'
    THERAPIE                    : THERAPIEEN(1) opposite of STAPPEN

AFSPRAKEN
description 'Afspraak voor behandelstap'
=
    datum                       : date
    tijdstip                    : timestamp
    locatie                     : text
    status                      : text
    BEHANDELSTAP                : BEHANDELSTAPPEN(1) required opposite of AFSPRAKEN

